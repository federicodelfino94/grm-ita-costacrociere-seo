config {
  type: "table",
  schema: "seo_dashboard_04_rpt",
  name: "seo_semrush_ranking",
}

with tmp as (
  select 
    Market
    , CASE 
        WHEN upper(trim(Market)) = 'IT' then 'Italy'
        WHEN upper(trim(Market)) = 'FR' then 'France'
        WHEN upper(trim(Market)) = 'ES' then 'Spain'
        WHEN upper(trim(Market)) = 'DE' then 'Germany'
        WHEN upper(trim(Market)) IN ('CH', 'CH-FR', 'CH-DE') then 'Switzerland'
        WHEN upper(trim(Market)) = 'AT' then 'Austria'
        WHEN upper(trim(Market)) = 'BR' then 'Brazil'
        WHEN upper(trim(Market)) = 'AR' then 'Argentina'
        WHEN upper(trim(Market)) = 'RU' then 'Russia'
        WHEN upper(trim(Market)) = 'UK' then 'UK'
      ELSE 'N/A'
    END as Country
    , Main_Cluster
    , Keywords
    , ifnull(SAFE_CAST(Keyword_Position as INT), 101) as Keyword_Position
    , Position_Label
    , SAFE_CAST(REPLACE(Visibility_Index, ',', '') as INT) as Visibility_Index
    , SAFE_CAST(Avg_Search_Volume_Monthly AS INT) as Avg_Search_Volume_Monthly
    , FORMAT_DATE("%B %Y", PARSE_DATE('%Y-%m', REPLACE(lower(ARRAY_REVERSE(SPLIT(_FILE_NAME, '_'))[SAFE_OFFSET(0)]) , '.csv', ''))) as date_str
	from ${ref("seo_dashboard_01_temp", "seo_semrush_ranking")}
)
select 
  tmp.*
  , c.ID_Country
from tmp
left join ${ref("seo_dashboard_04_rpt", "D_country")} c
on c.Country = tmp.Country