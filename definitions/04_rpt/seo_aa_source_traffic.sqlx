config {
  type: "table",
  schema: "seo_dashboard_04_rpt",
  name: "seo_aa_source_traffic",
  tags:["ADOBE"]
}

with tmp as (
  select
     day
      , date_trunc(day, month) as year_month
      , entry_page_url
      ,         case 
            when lower(segment_name) like '%direct%' then 'Direct'
            when lower(segment_name) like '%organic%' then 'Organic Search'
            when lower(segment_name) like '%referral%' then 'Referral'
        else 
            'N/A' 
        end as channel
      ,         case     
            when upper(segment_name) like '%==%IT%' then 'Italy'
            when upper(segment_name) like '%==%FR%' then 'France'
            when upper(segment_name) like '%==%CH%' then 'Switzerland'
            when upper(segment_name) like '%==%UK%' then 'UK'
            when upper(segment_name) like '%==%ES%' then 'Spain'
            when upper(segment_name) like '%==%AT%' then 'Austria'
            when upper(segment_name) like '%==%DE%' then 'Germany'
            when upper(segment_name) like '%==%AR%' then 'Argentina'
            when upper(segment_name) like '%==%BR%' then 'Brazil'
            when upper(segment_name) like '%==%RU%' then 'Russia'
            when upper(segment_name) like '%==%SE%' then 'Sweden'
            when upper(segment_name) like '%==%DK%' then 'Denmark'
            when upper(segment_name) like '%==%BE%' then 'Belgium'
            when upper(segment_name) like '%==%NL%' then 'Netherlands'
            when upper(segment_name) like '%==%FI%' then 'Finland'
            when upper(segment_name) like '%==%US%' then 'USA'
            else 
            'N/A' 
        end as country
      ,case 
        when entry_page_url like '%/costa-club/magazine/%' 
        then 'Magazine'
        else 'Ecommerce'
        end as flag_magazine
      , item_id
      , company_id
      , report_suite_id
      , segment_name
      , sum(visits) as visits
      , sum(total_orders) as orders
      , sum(page_views) as page_views
      , sum(bounces) as bounces
      , sum(unique_visitors) as unique_visitors
      , sum(new_visitors) as new_visitors
  from ${ref("seo_adobeanalytics_raw")} 
  group by
      day
      , year_month
      , entry_page_url
      , channel
      , country
      , flag_magazine
      , item_id
      , company_id
      , report_suite_id
      , segment_name
)

select 
  tmp.*
  , c.ID_Country
  , t.ID_Traffic_Type
from tmp 
join ${ref("D_country")} c
on c.Country = tmp.country
left join ${ref("D_traffic")} t
on t.Traffic_Type = tmp.channel
left join ${ref("seo_segment_exclude")} d
on tmp.segment_name= d.segment 
where d.segment is null
