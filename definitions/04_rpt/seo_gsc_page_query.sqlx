config {
  type: "incremental",
  name: "seo_gsc_page_query",
  schema: "seo_dashboard_04_rpt",
  dependencies: ["data_flow_insertion_log"],
  uniqueKey: ["year_month", "site", "page", "query"],
  assertions: {
    uniqueKey: ["year_month", "site", "page", "query"]
  },
  bigquery: {
    partitionBy: "DATE_TRUNC(year_month, MONTH)",
    clusterBy: ["site", "page", "query"]
  }
}

pre_operations {
  declare ts default (
    select MIN(file_upload_ts) 
    from ${ref("seo_dashboard_utils", "data_flow_insertion_log")}
    where ID_data_flow = 2 --data_flow_name = 'seo_gsc_page_query'
    and processed_flag = FALSE
  )
}

with tmp as (
  select
    cast(CONCAT(FORMAT_DATETIME("%Y-%m", day),'-01') as date) as year_month 
    , lower(site) as site
    , lower(page) as page
    , lower(trim(SUBSTRING(query, 0, 200))) as query
    , case 
        when lower(query) like '%costa%' then 'Brand' 
        when lower(query) like '%costacrociere%' then 'Brand' 
        when lower(query) like '%costa crociere%' then 'Brand' 
        when lower(query) like '%costakreuzfahrten%' then 'Brand' 
        when lower(query) like '%costa kreuzfahrten%' then 'Brand' 
        when lower(query) like '%costacroisieres%' then 'Brand' 
        when lower(query) like '%costa croisieres%' then 'Brand' 
        when lower(query) like '%costacruises%' then 'Brand' 
        when lower(query) like '%costa cruises%' then 'Brand' 
        when lower(query) like '%costacruceros%' then 'Brand' 
        when lower(query) like '%costa cruceros%' then 'Brand' 
        when lower(query) like '%costacruizeiros%' then 'Brand' 
        when lower(query) like '%costa cruizeiros%' then 'Brand' 
        else 'Generic'
      end as Brand_Generic_SuperGeneric
    , case
        when lower(site) like '%.it%' then 'Italy'
        when lower(site) like '%.fr%' then 'France' 
        when lower(site) like '%.ch%' then 'Switzerland'
        when lower(site) like '%.uk%' then 'UK'
        when lower(site) like '%.es%' then 'Spain'
        when lower(site) like '%.com/SE%' then 'Sweden'
        when lower(site) like '%.at%' then 'Austria'
        when lower(site) like '%.de%' then 'Germany'
        when lower(site) like '%www.costacruceros.com/%' then 'Argentina'
        when lower(site) like '%.dk%' then 'Denmark'
        when lower(site) like '%www.costacruzeiros.com/%' then 'Brazil'
        when lower(site) like '%costacruises.ru%' then 'Russia'
        else 'N/A'
      end as Country
    , sum(impressions) as impressions
    , sum(clicks) as clicks
    , avg(position) as position
  from ${ref("seo_dashboard_01_temp", "seo_gsc_page_query")}
  ${ when(
      incremental(),
      `WHERE dt_created >= ts`
    )
  }
  group by 
    cast(CONCAT(FORMAT_DATETIME("%Y-%m", day),'-01') as date) 
    , lower(site)
    , lower(page)
    , lower(trim(SUBSTRING(query, 0, 200)))
    , Country
    , Brand_Generic_SuperGeneric
)
select 
  tmp.*
  , c.ID_Country
from tmp 
left join ${ref("seo_dashboard_04_rpt", "D_country")} c
on c.Country = tmp.Country


post_operations {
  UPDATE ${ref("seo_dashboard_utils", "data_flow_insertion_log")}
  SET processed_flag = TRUE
  where file_upload_ts >= ts
  and ID_data_flow = 2 --data_flow_name = 'seo_gsc_page_query'
}