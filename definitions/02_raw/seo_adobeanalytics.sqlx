config {
  type: "incremental",
  name: "seo_adobeanalytics",
  schema: "seo_dashboard_02_raw",
  uniqueKey: ["company_id", "report_suite_id", "segment_name", "day", "item_id", "entry_page_url"],
  assertions: {
    uniqueKey: ["company_id", "report_suite_id", "segment_name", "day", "item_id", "entry_page_url"]
  },
  bigquery: {
    partitionBy: "day"
  },
  tags:["ADOBE"]
}

with up_to_date as (
  select
    * 
    , row_number() over (
        partition by 
          company_id
        , report_suite_id
        , segment_name
        , day
        , item_id
        , entry_page_url
        order by dt_created desc
    ) rn
  from ${ref("seo_dashboard_01_temp", "seo_adobeanalytics")}
  QUALIFY rn = 1
)

  select 
      company_id
      , report_suite_id
      , segment_name
      , day
      , item_id
      , entry_page_url
      , visits
      , total_orders
      , page_views
      , bounces
      , unique_visitors
      , new_visitors
      , ${gsc_adobe_segment_country()} as country
      , ${gsc_adobe_segment_channel()} as channel
  from up_to_date