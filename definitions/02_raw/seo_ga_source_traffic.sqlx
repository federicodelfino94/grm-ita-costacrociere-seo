config {
  type: "incremental",
  name: "seo_ga_source_traffic",
  schema: "seo_dashboard_02_raw",
  dependencies: ["temp seo_ga_source_traffic"],
  uniqueKey: ["gaaccountid","gadate","gaprofileid","gaprofilename","gawebpropertyid","gawebpropertyname","gasourcemedium","gadevicecategory","gachannelgrouping", "galandingpagepath"],
  assertions: {
    uniqueKey: ["gaaccountid","gadate","gaprofileid","gaprofilename","gawebpropertyid","gawebpropertyname","gasourcemedium","gadevicecategory","gachannelgrouping", "galandingpagepath"]
  },
  bigquery: {
    partitionBy: "gadate",
    clusterBy: ["gaprofileid", "gawebpropertyid", "gadevicecategory", "gachannelgrouping"]
  }
}


pre_operations {
  DECLARE ts DATETIME
  ---
  CREATE TEMP TABLE tmp_t (
    dt_filename STRING,
    file_upload_ts DATETIME
  ) AS 
    select dt_filename , file_upload_ts
    from ${ref("seo_dashboard_utils", "data_flow_insertion_log")}
    where ID_data_flow = 4 --data_flow_name = 'seo_ga_source_traffic'
    and processed_flag = FALSE
  ---
  set ts = (
    select ifnull(MIN(file_upload_ts), DATETIME_ADD(CURRENT_DATETIME(), INTERVAL 1 YEAR)) 
    from tmp_t
  )
}

with up_to_date as (
  select
    * 
    , row_number() over (
        partition by gaaccountid
                     , gadate
                     , gaprofileid
                     , gaprofilename
                     , gawebpropertyid
                     , gawebpropertyname
                     , gasourcemedium
                     , gadevicecategory
                     , gachannelgrouping
        order by dt_updated desc
    ) rank
  from ${ref("seo_dashboard_01_temp", "seo_ga_source_traffic")}
  ${ when(
      incremental(),
      `WHERE dt_created >= date(ts) 
       AND dt_filename in (select dt_filename from tmp_t)
      `
    )
  } 
)
select 
  * except (dt_updated, rank)
from up_to_date
where rank = 1


post_operations {
  UPDATE ${ref("seo_dashboard_utils", "data_flow_insertion_log")}
  SET processed_flag = TRUE
  where file_upload_ts >= ts
  and ID_data_flow = 4 --data_flow_name = 'seo_ga_source_traffic'
  AND dt_filename in (select dt_filename from tmp_t)
}